apply plugin: 'com.android.library'
apply plugin: 'bintray-release'

//Plugin for generating test coverage reports.
//They are generated by calling the following from the root folder:
// ./gradlew createDebugCoverageReport
apply plugin: 'jacoco-android'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "16.12.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testHandleProfiling true
        testFunctionalTest true
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    // This enables long timeouts required on slow environments, e.g. Travis
    adbOptions {
        timeOutInMs 20 * 60 * 1000  // 20 minutes
        installOptions "-d","-t"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.android.support:support-annotations:25.1.0"
    androidTestCompile 'junit:junit:4.12'

    androidTestCompile 'org.mockito:mockito-core:2.0.42-beta'

    androidTestCompile "org.powermock:powermock-core:1.6.5"
    androidTestCompile "org.powermock:powermock-module-junit4:1.6.5"
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

    //androidTestCompile 'org.assertj:assertj-core:3.6.2'
}

publish {
    userOrg = 'countly'
    groupId = 'ly.count.android'
    artifactId = 'sdk'
    version = '16.12.2'
    description = 'Android SDK for Countly mobile analytics'
    website = 'https://github.com/Countly/countly-sdk-android'
}
